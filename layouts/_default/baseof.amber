doctype 5
html[lang=Site.LanguageCode]
  head
    title
      block title
        | #{ Title }
        if Title != Site.Title
          |  â€¢ #{ Site.Title }

    meta[name="viewport"][content="width=device-width, initial-scale=1"]
    meta[http-equiv="x-ua-compatible"][content="ie-edge"]

    link[rel="canonical"][href=Permalink]

    block seo
      meta[name="robots"][content="index, follow"]

    #{ Hugo.Generator }

    block css
      link[rel="stylesheet"][href=absURL("css/main.css")]

  body
    block nav
      import ../partials/nav.amber

    block header
      import ../partials/header.amber

    block main
      div.container
        div.row
          div.col-xs-12.col-md-8
            block content
          div.col-xs-12.col-md-4
            block search
              import ../partials/search.amber
            block sidebar
              import ../partials/sidebar.amber

    block footer
      import ../partials/footer.amber

    block js
      script[data-src=absURL("js/components.js")][data-event="components"]
      script[async][src=absURL("js/main.js")]

    block currentLocation
      import ../mixins/geojson.amber
        +openCollection("current")

        $locations = last(1, Site.Data.locations.features)
        each $location in $locations
          +toGeoJSON(nil, nil, $location.geometry)
          | ,

          $countries = where($Site.Data.countries.features, "properties.slug", $location.properties.country.slug)
          each $country in $countries
            $geometry = index($country.geometry.geometries, 2)
            +toGeoJSON(nil, nil, $geometry)

        +closeCollection